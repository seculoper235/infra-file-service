plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.jooq.jooq-codegen-gradle" version "3.19.21"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'software.amazon.awssdk:s3:2.30.34'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    runtimeOnly 'org.postgresql:postgresql'

    jooqCodegen 'org.postgresql:postgresql'
    jooqCodegen "org.jooq:jooq-meta-extensions-liquibase:3.19.21"
    jooqCodegen "org.jooq:jooq:3.19.21"
    jooqCodegen "org.jooq:jooq-meta:3.19.21"
    jooqCodegen "org.jooq:jooq-codegen:3.19.21"

    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'io.vavr:vavr:0.10.5'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:localstack'
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}

jooq {
    configuration {
        generator {
            database {
                name = "org.jooq.meta.extensions.liquibase.LiquibaseDatabase"

                properties {
                    property {
                        key = "rootPath"
                        value = "${projectDir}/src/main/resources"
                    }

                    property {
                        key = "scripts"
                        value = "db/changelog.yaml"
                    }

                    property {
                        key = "includeLiquibaseTables"
                        value = true
                    }

                    property {
                        key = "useParsingConnection"
                        value = false
                    }

                    property {
                        key = "changeLogParameters.contexts"
                        value = "!test"
                    }
                }
            }
        }
    }
}

tasks.named('compileJava').configure {
    it.dependsOn(tasks.named('jooqCodegen'))
}

tasks.named('compileTestJava').configure {
    it.dependsOn(tasks.named('jooqCodegen'))
}
